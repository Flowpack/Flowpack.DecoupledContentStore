prototype(Flowpack.DecoupledContentStore:Controller.Backend.Index) < prototype(Neos.Fusion:Component) {

    // API, list of ContentReleaseOverviewRow objects
    overviewData = null

    renderer = Neos.Fusion:Component {
        _renderedTableBody = Neos.Fusion:Loop {
            items = ${props.overviewData}
            itemRenderer = Neos.Fusion:Component {
                // item instanceof ContentReleaseOverviewRow
                contentReleaseIdentifier = ${item.contentReleaseIdentifier.identifier}

                enumeratedDocumentNodesCount = ${item.enumeratedDocumentNodesCount}
                start = ${Date.format(item.metadata.startTime, 'd.m.Y H:i:s')}
                end = ${item.metadata.endTime ? Date.format(item.metadata.endTime, 'd.m.Y H:i:s') : ''}
                switch = ${item.metadata.switchTime ? Date.format(item.metadata.switchTime, 'd.m.Y H:i:s') : ''}

                renderer = afx`
                    <tr>
                        <td>
                            <Neos.Fusion:Link.Action href.action="details" href.arguments={{contentReleaseIdentifier: props.contentReleaseIdentifier}}>
                                {props.contentReleaseIdentifier}
                            </Neos.Fusion:Link.Action>
                            {" "}
                            <span class="neos-badge" title="status" data-neos-toggle="tooltip">{item.metadata.status.displayName}</span>
                            <span @if.errors={item.ErrorCount} class="neos-badge neos-badge-important" title="errors" data-neos-toggle="tooltip">{item.errorCount}</span>
                        </td>
                        <td>
                            {props.enumeratedDocumentNodesCount}
                        </td>
                        <td>
                            <span @if.isRunning={item.metadata.status.displayName == "running"}>{item.progress}%</span>
                            <span @if.isNotRunning={item.metadata.status.displayName != "running"}>
                                <span class="neos-badge neos-badge-success" title="rendered" data-neos-toggle="tooltip">{item.renderedUrlCount}</span>
                                / <span class="neos-badge" title="total" data-neos-toggle="tooltip">{props.enumeratedDocumentNodesCount}</span>
                            </span>
                        </td>
                        <td>{item.iterationsCount}</td>
                        <td>{props.start}</td>
                        <td>{props.end}</td>
                        <td>{props.switch}</td>
                        <td>TODO</td>
                    </tr>
                `
            }
        }
        renderer = afx`
            <form id="postHelper" method="post">
                <input
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                />
            </form>
            <div class="neos-content neos-container-fluid">
                <legend>Content Store Releases</legend>
                <div class="neos-row-fluid">
                    <table class="neos-table cs-releases-table">
                        <thead>
                        <tr>
                            <th style="width: 240px">Identifier</th>
                            <th style="width: 200px">Page Count</th>
                            <th style="width: 200px">Render Progress</th>
                            <th style="width: 200px">Iterations</th>
                            <th>Start</th>
                            <th>Stop</th>
                            <th>Switch</th>
                            <th>Release Size</th>
                            <th>&nbsp;</th>
                        </tr>
                        </thead>
                        <tbody>
                            {props._renderedTableBody}
                        </tbody>
                    </table>

                    <Flowpack.DecoupledContentStore:ListFooter />
                </div>
            </div>
        `
    }
    @process.debugDump = Neos.Fusion:DebugDump
}

prototype(Flowpack.DecoupledContentStore:ListFooter) < prototype(Neos.Fusion:Component) {
    _publishAllUri = Neos.Fusion:UriBuilder {
        action = 'publishAll'
    }
    renderer = afx`
        <div class="neos-footer">
            <Neos.Fusion:Link.Action href.action="index" attributes.class="neos-button">
                <i class="fa fa-sync"></i> Reload
            </Neos.Fusion:Link.Action>

            <button form="postHelper" formaction={props._publishAllUri} type="submit" class="neos-button neos-button-warning neos-pull-right">
                Publish All
            </button>
        </div>
    `
}
